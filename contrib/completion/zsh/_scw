#compdef scw
#
# zsh completion for scw (http://scaleway.com)
#
# Inspired by https://github.com/felixr/docker-zsh-completion

__scw_get_servers() {
    local expl
    declare -a servers
    servers=(${(f)"$(_call_program commands scw _completion servers-names)"})
    _describe -t servers "servers" servers
}

__scw_stoppedservers() {
    __scw_get_servers
}

__scw_runningservers() {
    __scw_get_servers
}

__scw_servers () {
    __scw_get_servers
}

__scw_images () {
    local expl
    declare -a images
    images=(${(f)"$(_call_program commands scw _completion images-names)"})
    _describe -t images "images" images
}

__scw_tags() {
    __scw_images
}

__scw_repositories_with_tags() {
    __scw_images
}

__scw_search() {
    # declare -a scwsearch
    local cache_policy
    zstyle -s ":completion:${curcontext}:" cache-policy cache_policy
    if [[ -z "$cache_policy" ]]; then
        zstyle ":completion:${curcontext}:" cache-policy __scw_caching_policy
    fi

    local searchterm cachename
    searchterm="${words[$CURRENT]%/}"
    cachename=_scw-search-$searchterm

    local expl
    local -a result
    if ( [[ ${(P)+cachename} -eq 0 ]] || _cache_invalid ${cachename#_} ) \
        && ! _retrieve_cache ${cachename#_}; then
        _message "Searching for ${searchterm}..."
        result=(${${${(f)"$(_call_program commands scw search ${searchterm})"}%% *}[2,-1]})
        _store_cache ${cachename#_} result
    fi
    _wanted scwsearch expl 'available images' compadd -a result
}

__scw_caching_policy()
{
  oldp=( "$1"(Nmh+1) )     # 1 hour
  (( $#oldp ))
}


__scw_repositories () {
    __scw_images
}

__scw_commands () {
    # local -a  _scw_subcommands
    local cache_policy

    zstyle -s ":completion:${curcontext}:" cache-policy cache_policy
    if [[ -z "$cache_policy" ]]; then
        zstyle ":completion:${curcontext}:" cache-policy __scw_caching_policy
    fi

    if ( [[ ${+_scw_subcommands} -eq 0 ]] || _cache_invalid scw_subcommands) \
        && ! _retrieve_cache scw_subcommands;
    then
        local -a lines
        lines=(${(f)"$(_call_program commands scw 2>&1)"})
        _scw_subcommands=(${${${lines[$((${lines[(i)Commands:]} + 1)),${lines[(I)    *]}]}## #}/ ##/:})
        _scw_subcommands=($_scw_subcommands 'help:Show help for a command')
        _store_cache scw_subcommands _scw_subcommands
    fi
    _describe -t scw-commands "scw command" _scw_subcommands
}

__scw_subcommand () {
    local -a _command_args
    case "$words[1]" in
        (attach)
            _arguments \
                '--no-stdin[Do not attach stdin]' \
                '--sig-proxy[Proxy all received signals to the process (non-TTY mode only)]' \
                ':servers:__scw_runningservers'
            ;;
        (build)
            _arguments \
                {-f,--file=-}'[Scwfile to use]:Scwfile:_files' \
                '--force-rm[Always remove intermediate servers]' \
                '--no-cache[Do not use cache when building the image]' \
                '--pull[Attempt to pull a newer version of the image]' \
                {-q,--quiet}'[Suppress verbose build output]' \
                '--rm[Remove intermediate servers after a successful build]' \
                {-t,--tag=-}'[Repository, name and tag to be applied]:repository:__scw_repositories_with_tags' \
                ':path or URL:_directories'
            ;;
        (commit)
            _arguments \
                {-a,--author=-}'[Author]:author: ' \
                {-m,--message=-}'[Commit message]:message: ' \
                {-p,--pause}'[Pause server during commit]' \
                ':server:__scw_servers' \
                ':repository:__scw_repositories_with_tags'
            ;;
        (cp)
            _arguments \
                ':server:->server' \
                ':hostpath:_files'
            case $state in
                (server)
                    if compset -P '*:'; then
                        _files
                    else
                        __scw_servers -qS ":"
                    fi
                    ;;
            esac
            ;;
        (diff|export)
            _arguments '*:servers:__scw_servers'
            ;;
        (events)
            _arguments \
                '*'{-f,--filter=-}'[Filter values]:filter: ' \
                '--since=-[Events created since this timestamp]:timestamp: ' \
                '--until=-[Events created until this timestamp]:timestamp: '
            ;;
        (exec)
            local state ret
            _arguments \
                {-d,--detach}'[Detached mode: leave the server running in the background]' \
                {-i,--interactive}'[Keep stdin open even if not attached]' \
                {-t,--tty}'[Allocate a pseudo-tty]' \
                ':servers:__scw_runningservers' \
                '*::command:->anycommand' && ret=0

            case $state in
                (anycommand)
                    shift 1 words
                    (( CURRENT-- ))
                    _normal
                    ;;
            esac

            return ret
            ;;
        (history)
            _arguments \
                '--no-trunc[Do not truncate output]' \
                {-q,--quiet}'[Only show numeric IDs]' \
                '*:images:__scw_images'
            ;;
        (images)
            _arguments \
                {-a,--all}'[Show all images]' \
                '*'{-f,--filter=-}'[Filter values]:filter: ' \
                '--no-trunc[Do not truncate output]' \
                {-q,--quiet}'[Only show numeric IDs]' \
                ':repository:__scw_repositories'
            ;;
        (import)
            _arguments \
                ':URL:(- http:// file://)' \
                ':repository:__scw_repositories_with_tags'
            ;;
        (info)
            ;;
        (inspect)
            _arguments \
                {-f,--format=-}'[Format the output using the given go template]:template: ' \
                '*:servers:__scw_servers'
            ;;
        (kill)
            _arguments \
                {-s,--signal=-}'[Signal to send]:signal:_signals' \
                '*:servers:__scw_runningservers'
            ;;
        (load)
            _arguments \
                {-i,--input=-}'[Read from tar archive file]:archive file:_files -g "*.((tar|TAR)(.gz|.GZ|.Z|.bz2|.lzma|.xz|)|(tbz|tgz|txz))(-.)"'
            ;;
        (login)
            _arguments \
                {-e,--email=-}'[Email]:email: ' \
                {-p,--password=-}'[Password]:password: ' \
                {-u,--user=-}'[Username]:username: ' \
                ':server: '
            ;;
        (logout)
            _arguments \
                ':server: '
            ;;
        (logs)
            _arguments \
                {-f,--follow}'[Follow log output]' \
                '-s,--since[Show logs since timestamp]' \
                {-t,--timestamps}'[Show timestamps]' \
                '--tail=-[Output the last K lines]:lines:(1 10 20 50 all)' \
                '*:servers:__scw_servers'
            ;;
        (port)
            _arguments \
                '1:servers:__scw_runningservers' \
                '2:port:_ports'
            ;;
        (pause|unpause)
            _arguments \
                '1:servers:__scw_runningservers'
            ;;
        (start)
            _arguments \
                {-a,--attach}'[Attach server'"'"'s stdout/stderr and forward all signals]' \
                {-i,--interactive}'[Attach server'"'"'s stding]' \
                '*:servers:__scw_stoppedservers'
            ;;
        (stats)
            _arguments \
                '--no-stream[Disable streaming stats and only pull the first result]' \
                '*:servers:__scw_runningservers'
            ;;
        (rm)
            _arguments \
                {-f,--force}'[Force removal]' \
                {-l,--link}'[Remove the specified link and not the underlying server]' \
                {-v,--volumes}'[Remove the volumes associated to the server]' \
                '*:servers:__scw_stoppedservers'
            ;;
        (rmi)
            _arguments \
                {-f,--force}'[Force removal]' \
                '--no-prune[Do not delete untagged parents]' \
                '*:images:__scw_images'
            ;;
        (restart|stop)
            _arguments \
                {-t,--time=-}'[Number of seconds to try to stop for before killing the server]:seconds to before killing:(1 5 10 30 60)' \
                '*:servers:__scw_runningservers'
            ;;
        (top)
            _arguments \
                '1:servers:__scw_runningservers' \
                '(-)*:: :->ps-arguments'
            case $state in
                (ps-arguments)
                    _ps
                    ;;
            esac

            ;;
        (ps)
            _arguments \
                {-a,--all}'[Show all servers]' \
                '--before=-[Show only server created before...]:servers:__scw_servers' \
                '*'{-f,--filter=-}'[Filter values]:filter: ' \
                {-l,--latest}'[Show only the latest created server]' \
                '-n[Show n last created servers, include non-running one]:n:(1 5 10 25 50)' \
                '--no-trunc[Do not truncate output]' \
                {-q,--quiet}'[Only show numeric IDs]' \
                {-s,--size}'[Display total file sizes]' \
                '--since=-[Show only servers created since...]:servers:__scw_servers'
            ;;
        (tag)
            _arguments \
                {-f,--force}'[force]'\
                ':image:__scw_images'\
                ':repository:__scw_repositories_with_tags'
            ;;
        (create|run)
            _arguments \
                {-a,--attach}'[Attach to stdin, stdout or stderr]' \
                '*--add-host=-[Add a custom host-to-IP mapping]:host\:ip mapping: ' \
                {-c,--cpu-shares=-}'[CPU shares (relative weight)]:CPU shares:(0 10 100 200 500 800 1000)' \
                '*--cap-add=-[Add Linux capabilities]:capability: ' \
                '*--cap-drop=-[Drop Linux capabilities]:capability: ' \
                '--cidfile=-[Write the server ID to the file]:CID file:_files' \
                '--cpuset=-[CPUs in which to allow execution]:CPU set: ' \
                {-d,--detach}'[Detached mode: leave the server running in the background]' \
                '*--device=-[Add a host device to the server]:device:_files' \
                '*--dns=-[Set custom dns servers]:dns server: ' \
                '*--dns-search=-[Set custom DNS search domains]:dns domains: ' \
                '*'{-e,--environment=-}'[Set environment variables]:environment variable: ' \
                '--entrypoint=-[Overwrite the default entrypoint of the image]:entry point: ' \
                '*--env-file=-[Read environment variables from a file]:environment file:_files' \
                '*--expose=-[Expose a port from the server without publishing it]: ' \
                {-h,--hostname=-}'[Server host name]:hostname:_hosts' \
                {-i,--interactive}'[Keep stdin open even if not attached]' \
                '*--link=-[Add link to another server]:link:->link' \
                '*--lxc-conf=-[Add custom lxc options]:lxc options: ' \
                '-m[Memory limit (in bytes)]:limit: ' \
                '--name=-[Server name]:name: ' \
                '--net=-[Network mode]:network mode:(bridge none server host)' \
                {-P,--publish-names}'[Publish all exposed ports]' \
                '*'{-p,--publish=-}'[Expose a server'"'"'s port to the host]:port:_ports' \
                '--privileged[Give extended privileges to this server]' \
                '--restart=-[Restart policy]:restart policy:(no on-failure always)' \
                '--rm[Remove intermediate servers when it exits]' \
                '*--security-opt=-[Security options]:security option: ' \
                '--sig-proxy[Proxy all received signals to the process (non-TTY mode only)]' \
                {-t,--tty}'[Allocate a pseudo-tty]' \
                {-u,--user=-}'[Username or UID]:user:_users' \
                '*-v[Bind mount a volume]:volume: '\
                '*--volumes-from=-[Mount volumes from the specified server]:volume: ' \
                {-w,--workdir=-}'[Working directory inside the server]:directory:_directories' \
                '(-):images:__scw_images' \
                '(-):command: _command_names -e' \
                '*::arguments: _normal'

            case $state in
                (link)
                    if compset -P '*:'; then
                        _wanted alias expl 'Alias' compadd -E ""
                    else
                        __scw_runningservers -qS ":"
                    fi
                    ;;
            esac

            ;;
        (pull)
            _arguments \
                {-a,--all-tags}'[Download all tagged images]' \
                ':name:__scw_search'
            ;;
        (push)
            _arguments ':images:__scw_images'
            ;;
        (rename)
            _arguments \
                ':old name:__scw_servers' \
                ':new name: '
            ;;
        (save)
            _arguments \
                {-o,--output=-}'[Write to file]:file:_files' \
                '*:images:__scw_images'
            ;;
        (search)
            _arguments \
                '--automated[Only show automated builds]' \
                '--no-trunc[Do not truncate output]' \
                {-s,--stars=-}'[Only display with at least X stars]:stars:(0 10 100 1000)' \
                ':term: '
            ;;
        (wait)
            _arguments '*:servers:__scw_runningservers'
            ;;
        (help)
            _arguments ':subcommand:__scw_commands'
            ;;
        (*)
            _message 'Unknown sub command'
    esac

}

_scw () {
    # Support for subservices, which allows for `compdef _scw scw-shell=_scw_servers`.
    # Based on /usr/share/zsh/functions/Completion/Unix/_git without support for `ret`.
    if [[ $service != scw ]]; then
        _call_function - _$service
        return
    fi

    local curcontext="$curcontext" state line
    typeset -A opt_args

    _arguments -C \
      '-H[tcp://host:port to bind/connect to]:socket: ' \
         '(-): :->command' \
         '(-)*:: :->option-or-argument'

    if (( CURRENT == 1 )); then

    fi
    case $state in
        (command)
            __scw_commands
            ;;
        (option-or-argument)
            curcontext=${curcontext%:*:*}:scw-$words[1]:
            __scw_subcommand
            ;;
    esac
}

_scw "$@"

# Local Variables:
# mode: Shell-Script
# sh-indentation: 4
# indent-tabs-mode: nil
# sh-basic-offset: 4
# End:
# vim: ft=zsh sw=4 ts=4 et
